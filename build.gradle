buildscript {
    ext {
        //spring-cloud-dependencies 2020.0.0 默认不在加载bootstrap 配置文件，
        //如果项目中要用bootstrap 配置文件 需要手动添加spring-cloud-starter-bootstrap 依赖，不然启动项目会报错的。
        set('springCloudVersion', "2021.0.3")
        //定义一个变量，统一规定springboot的版本
        set('springBootVersion', "2.6.8")
        set('alibabaVersion', "2.2.7.RELEASE")
        set('lombok', "1.18.16")
        set('knife4j', "2.0.9")
        set('hutool', "4.6.3")
        set('rocketmq', "2.2.2")
    }
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }

    dependencies {
        //用来打包
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    //下面两句必须在所有子项目中添加，否则导致import了看起来没问题，但是编译时找不到其他模块的类。
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    // 指定JDK版本
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }

    //指定编码格式
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}


subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    //dependency-management 插件
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation(enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"))
        implementation(enforcedPlatform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:${alibabaVersion}"))
        implementation(enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
        implementation "com.github.xiaoymin:knife4j-spring-boot-starter:${knife4j}"
        implementation "cn.hutool:hutool-all:${hutool}"
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config'
        implementation('com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery'){
            exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter-netflix-ribbon'
        }
        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
        compileOnly("org.projectlombok:lombok:${lombok}")
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor("org.projectlombok:lombok:${lombok}")
        testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

}



